plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/magna.accessWidener")
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.jamieswhiteshirt.com/libs-release/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // config
    modImplementation 'com.github.Draylar.omega-config:omega-config-base:1.0.8-1.17'
    include 'com.github.Draylar.omega-config:omega-config-base:1.0.8-1.17'

    // dev Reach Entity Attributes for testing tools with extended reach (or players)
    // reach-entity-attributes has not yet been ported to 1.20.1
    // modImplementation "com.jamieswhiteshirt:reach-entity-attributes:${project.rea_version}"
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

dependencies {
    afterEvaluate {
        testmodImplementation sourceSets.main.output
    }
}

loom {
    runs {
        testmodClient {
            client()
            name = "Test Mod Client"
            source sourceSets.testmod
        }

        testmodServer {
            server()
            name = "Test Mod Server"
            source sourceSets.testmod
        }
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
     withSourcesJar()
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "draylarRepository"
            url = "https://maven.draylar.dev/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}